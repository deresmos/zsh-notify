# vim: set nowrap filetype=zsh:
#
# terminal-is-active exits with code 0 when the current shell is running on an
# active terminal window or tab, code 1 when the window or tab is in background
# and 2 if it's unable to query the terminal state.
() {
    # Exit with 0 if inside a TMUX pane
    function is-inside-tmux {
        [ "$TMUX" != "" ]
    }

    # Find the TTY for the current shell, also accounting for TMUX.
    function current-tty {
        if is-inside-tmux; then
            tmux display-message -p '#{client_tty}'
        else
            echo $TTY
        fi
    }

    # Exit with 0 if given TMUX pane is the active one.
    function is-current-tmux-pane-active {
        is-inside-tmux || return 1

        local active_pane_id=$(tmux list-windows -F '#{window_active} #{pane_id}' | grep -i '^1' | awk '{ print $2 }')

        if [[ "$TMUX_PANE" == "$active_pane_id" ]]; then
            return 0
        fi

        return 1
    }

    # Run an AppleScript, selected from ./resources by basename given as the first argument,
    # with all other arguments are positional arguments to the script's `on run` handle.
    function run-applescript() {
      local script_name resource_dir

      zstyle -s ':notify:' resources-dir resources_dir
      
      script_name="$1"
      shift
      
      "$resources_dir"/"$script_name".applescript $@ 2>/dev/null
    }

    # Exit with code 0 if the terminal window/tab is active, code 1 if inactive.
    function is-terminal-window-active {
        local query_tool

        zstyle -s ':notify:' query-tool query_tool

        case "$query_tool" in
            "iterm2"|"apple-terminal")
                run-applescript is-"$query_tool"-active "$(current-tty)" \
                  && return 0
            ;;
            "xdotool")
                local active_wid wid

                active_wid=$(xdotool getactivewindow)

                zstyle -s ':notify:' window-id wid
  
                if [[ "$active_wid" == "$wid" ]]; then
                    return 0
                fi
            ;;
        esac

        return 1
    }

    if is-terminal-window-active; then
        if is-inside-tmux; then
            is-current-tmux-pane-active
            return $?
        fi
      else
        return $?
    fi
}
